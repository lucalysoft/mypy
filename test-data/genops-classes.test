[case testGetAttribute]
class A:
    x: int

def f(a: A) -> int:
    return a.x
[out]
def f(a):
    a :: A
    r0 :: int
L0:
    r0 = a.x
    return r0

[case testSetAttribute]
class A:
    x: int

def f(a: A) -> None:
    a.x = 1
[out]
def f(a):
    a :: A
    r0 :: int
    r1 :: bool
    r2 :: None
L0:
    r0 = 1
    a.x = r0; r1 = is_error
    r2 = None
    return r2

[case testUserClassInList]
class C:
    x: int

def f() -> int:
    c = C()
    c.x = 5
    a = [c]
    d = a[0]
    return d.x + 1
[out]
def f():
    r0, c :: C
    r1 :: int
    r2 :: bool
    r3, a :: list
    r4 :: int
    r5 :: object
    r6, d :: C
    r7, r8, r9 :: int
L0:
    r0 = C()
    c = r0
    r1 = 5
    c.x = r1; r2 = is_error
    r3 = [c]
    a = r3
    r4 = 0
    r5 = a[r4] :: list
    r6 = cast(C, r5)
    d = r6
    r7 = d.x
    r8 = 1
    r9 = r7 + r8 :: int
    return r9

[case testMethodCall]
class A:
    def f(self, x: int, y: str) -> int:
        return x + 10

def g(a: A) -> None:
    a.f(1, 'hi')
[out]
def A.f(self, x, y):
    self :: A
    x :: int
    y :: str
    r0, r1 :: int
L0:
    r0 = 10
    r1 = x + r0 :: int
    return r1
def g(a):
    a :: A
    r0 :: int
    r1 :: str
    r2 :: int
    r3 :: None
L0:
    r0 = 1
    r1 = unicode_2 :: static  ('hi')
    r2 = a.f(r0, r1)
    r3 = None
    return r3

[case testForwardUse]
def g(a: A) -> int:
    return a.n

class A:
    n : int

[out]
def g(a):
    a :: A
    r0 :: int
L0:
    r0 = a.n
    return r0

[case testOptionalMember]
from typing import Optional
class Node:
    next: Optional[Node]
    def length(self) -> int:
        if self.next is not None:
            return 1 + self.next.length()
        return 1
[out]
def Node.length(self):
    self :: Node
    r0 :: union[Node, None]
    r1 :: None
    r2, r3 :: bool
    r4 :: int
    r5 :: union[Node, None]
    r6 :: Node
    r7, r8, r9 :: int
L0:
    r0 = self.next
    r1 = None
    r2 = r0 is r1
    r3 = !r2
    if r3 goto L1 else goto L2 :: bool
L1:
    r4 = 1
    r5 = self.next
    r6 = cast(Node, r5)
    r7 = r6.length()
    r8 = r4 + r7 :: int
    return r8
L2:
    r9 = 1
    return r9

[case testSubclass]
class A:
    def __init__(self) -> None:
        self.x = 10
class B(A):
    def __init__(self) -> None:
        self.x = 20
        self.y = 30
[out]
def A.__init__(self):
    self :: A
    r0 :: int
    r1 :: bool
    r2 :: None
L0:
    r0 = 10
    self.x = r0; r1 = is_error
    r2 = None
    return r2
def B.__init__(self):
    self :: B
    r0 :: int
    r1 :: bool
    r2 :: int
    r3 :: bool
    r4 :: None
L0:
    r0 = 20
    self.x = r0; r1 = is_error
    r2 = 30
    self.y = r2; r3 = is_error
    r4 = None
    return r4

[case testAttrLvalue]
class O(object):
    def __init__(self) -> None:
        self.x = 1

def increment(o: O) -> O:
    o.x += 1
    return o
[out]
def O.__init__(self):
    self :: O
    r0 :: int
    r1 :: bool
    r2 :: None
L0:
    r0 = 1
    self.x = r0; r1 = is_error
    r2 = None
    return r2
def increment(o):
    o :: O
    r0, r1, r2 :: int
    r3 :: bool
L0:
    r0 = o.x
    r1 = 1
    r2 = r0 += r1 :: int
    o.x = r2; r3 = is_error
    return o

[case testSubclassSpecialize2]
class A:
    def foo(self, x: int) -> object:
        return str(x)
class B(A):
    def foo(self, x: object) -> object:
        return x
class C(B):
    def foo(self, x: object) -> int:
        return id(x)

def use_a(x: A, y: int) -> object:
    return x.foo(y)

def use_b(x: B, y: object) -> object:
    return x.foo(y)

def use_c(x: C, y: object) -> int:
    return x.foo(y)
[out]
def A.foo(self, x):
    self :: A
    x :: int
    r0 :: object
    r1 :: str
L0:
    r0 = box(int, x)
    r1 = str r0 :: object
    return r1
def B.foo(self, x):
    self :: B
    x :: object
L0:
    return x
def B.foo__A_glue(self, x):
    self :: B
    x :: int
    r0, r1 :: object
L0:
    r0 = box(int, x)
    r1 = self.foo(r0)
    return r1
def C.foo(self, x):
    self :: C
    x, r0 :: object
    r1 :: str
    r2, r3 :: object
    r4 :: int
L0:
    r0 = builtins.module :: static
    r1 = unicode_4 :: static  ('id')
    r2 = getattr r0, r1
    r3 = py_call(r2, x)
    r4 = unbox(int, r3)
    return r4
def C.foo__B_glue(self, x):
    self :: C
    x :: object
    r0 :: int
    r1 :: object
L0:
    r0 = self.foo(x)
    r1 = box(int, r0)
    return r1
def C.foo__A_glue(self, x):
    self :: C
    x :: int
    r0 :: object
    r1 :: int
    r2 :: object
L0:
    r0 = box(int, x)
    r1 = self.foo(r0)
    r2 = box(int, r1)
    return r2
def use_a(x, y):
    x :: A
    y :: int
    r0 :: object
L0:
    r0 = x.foo(y)
    return r0
def use_b(x, y):
    x :: B
    y, r0 :: object
L0:
    r0 = x.foo(y)
    return r0
def use_c(x, y):
    x :: C
    y :: object
    r0 :: int
L0:
    r0 = x.foo(y)
    return r0

[case testSubclass_toplevel]
from typing import TypeVar, Generic
from mypy_extensions import trait
T = TypeVar('T')
class C:
    pass

@trait
class S:
    pass

class D(C, S, Generic[T]):
    pass

[out]
def __top_level__():
    r0 :: str
    r1 :: object
    r2 :: str
    r3, r4, r5 :: object
    r6 :: str
    r7 :: bool
    r8 :: object
    r9 :: str
    r10, r11 :: object
    r12 :: bool
    r13 :: object
    r14 :: str
    r15 :: dict
    r16 :: bool
    r17 :: object
    r18 :: str
    r19, r20 :: object
    r21 :: bool
    r22 :: object
    r23 :: str
    r24 :: dict
    r25 :: bool
    r26, r27, r28 :: object
    r29 :: str
    r30, r31 :: object
    r32 :: str
    r33, r34 :: object
    r35 :: tuple[object, object, object]
    r36 :: object
    r37 :: str
    r38, r39 :: object
    r40 :: bool
    r41 :: object
    r42 :: str
    r43 :: dict
    r44 :: bool
    r45 :: None
L0:
    r0 = unicode_0 :: static  ('T')
    r1 = __main__.globals :: static
    r2 = unicode_1 :: static  ('TypeVar')
    r3 = r1[r2] :: dict
    r4 = py_call(r3, r0)
    r5 = __main__.globals :: static
    r6 = unicode_0 :: static  ('T')
    r7 = r5.__setitem__(r6, r4) :: object
    r8 = <error> :: object
    r9 = unicode_2 :: static  ('__main__')
    r10 = __main__.C_template :: type
    r11 = pytype_from_template(r10, r8, r9)
    r12 = C_trait_vtable_setup()
    __main__.C = r11 :: type
    r13 = __main__.globals :: static
    r14 = unicode_3 :: static  ('C')
    r15 = cast(dict, r13)
    r16 = r15.__setitem__(r14, r11) :: dict
    r17 = <error> :: object
    r18 = unicode_2 :: static  ('__main__')
    r19 = __main__.S_template :: type
    r20 = pytype_from_template(r19, r17, r18)
    r21 = S_trait_vtable_setup()
    __main__.S = r20 :: type
    r22 = __main__.globals :: static
    r23 = unicode_4 :: static  ('S')
    r24 = cast(dict, r22)
    r25 = r24.__setitem__(r23, r20) :: dict
    r26 = __main__.C :: type
    r27 = __main__.S :: type
    r28 = __main__.globals :: static
    r29 = unicode_5 :: static  ('Generic')
    r30 = r28[r29] :: dict
    r31 = __main__.globals :: static
    r32 = unicode_0 :: static  ('T')
    r33 = r31[r32] :: dict
    r34 = r30[r33] :: object
    r35 = (r26, r27, r34)
    r36 = box(tuple[object, object, object], r35)
    r37 = unicode_2 :: static  ('__main__')
    r38 = __main__.D_template :: type
    r39 = pytype_from_template(r38, r36, r37)
    r40 = D_trait_vtable_setup()
    __main__.D = r39 :: type
    r41 = __main__.globals :: static
    r42 = unicode_6 :: static  ('D')
    r43 = cast(dict, r41)
    r44 = r43.__setitem__(r42, r39) :: dict
    r45 = None
    return r45

[case testIsInstance]
class A: pass
class B(A): pass

def f(x: A) -> B:
    if isinstance(x, B):
        return x
    return B()
[out]
def f(x):
    x :: A
    r0 :: object
    r1 :: bool
    r2, r3 :: B
L0:
    r0 = __main__.B :: type
    r1 = isinstance x, r0
    if r1 goto L1 else goto L2 :: bool
L1:
    r2 = cast(B, x)
    return r2
L2:
    r3 = B()
    return r3

[case testFakeSuper]
class A:
    def __init__(self, x: int) -> None:
        self.x = x
class B(A):
    def __init__(self, x: int, y: int) -> None:
        A.__init__(self, x)
        self.y = y
[out]
def A.__init__(self, x):
    self :: A
    x :: int
    r0 :: bool
    r1 :: None
L0:
    self.x = x; r0 = is_error
    r1 = None
    return r1
def B.__init__(self, x, y):
    self :: B
    x, y :: int
    r0 :: None
    r1 :: bool
    r2 :: None
L0:
    r0 = A.__init__(self, x)
    self.y = y; r1 = is_error
    r2 = None
    return r2

[case testClassMethod]
class C:
    @staticmethod
    def foo(x: int) -> int: return 10 + x
    @classmethod
    def bar(cls, x: int) -> int: return 10 + x

def lol() -> int:
    return C.foo(1) + C.bar(2)
[out]
def C.foo(x):
    x, r0, r1 :: int
L0:
    r0 = 10
    r1 = r0 + x :: int
    return r1
def C.bar(cls, x):
    cls :: object
    x, r0, r1 :: int
L0:
    r0 = 10
    r1 = r0 + x :: int
    return r1
def lol():
    r0, r1 :: int
    r2 :: object
    r3, r4, r5 :: int
L0:
    r0 = 1
    r1 = C.foo(r0)
    r2 = __main__.C :: type
    r3 = 2
    r4 = C.bar(r2, r3)
    r5 = r1 + r4 :: int
    return r5

[case testSuper1]
class A:
    def __init__(self, x: int) -> None:
        self.x = x
class B(A):
    def __init__(self, x: int, y: int) -> None:
        super().__init__(x)
        self.y = y
[out]
def A.__init__(self, x):
    self :: A
    x :: int
    r0 :: bool
    r1 :: None
L0:
    self.x = x; r0 = is_error
    r1 = None
    return r1
def B.__init__(self, x, y):
    self :: B
    x, y :: int
    r0 :: None
    r1 :: bool
    r2 :: None
L0:
    r0 = A.__init__(self, x)
    self.y = y; r1 = is_error
    r2 = None
    return r2

[case testSuper2]
from mypy_extensions import trait
@trait
class T:
    def foo(self) -> None: pass

class X(T):
    def foo(self) -> None:
        super().foo()
[out]
def T.foo(self):
    self :: T
    r0 :: None
L0:
    r0 = None
    return r0
def X.foo(self):
    self :: X
    r0, r1 :: None
L0:
    r0 = T.foo(self)
    r1 = None
    return r1

[case testClassVariable]
from typing import ClassVar
class A:
    x = 10  # type: ClassVar[int]

def f() -> int:
    return A.x
[out]
def f():
    r0 :: object
    r1 :: str
    r2 :: object
    r3 :: int
L0:
    r0 = __main__.A :: type
    r1 = unicode_2 :: static  ('x')
    r2 = getattr r0, r1
    r3 = unbox(int, r2)
    return r3

[case testDefaultVars]
from typing import ClassVar, Optional
class A:
    x = 10
    def lol(self) -> None:
        self.x = 100

LOL = 'lol'
class B(A):
    y = LOL
    z: Optional[str] = None
    b = True
    bogus = None  # type: int
[out]
def A.lol(self):
    self :: A
    r0 :: int
    r1 :: bool
    r2 :: None
L0:
    r0 = 100
    self.x = r0; r1 = is_error
    r2 = None
    return r2
def A.__mypyc_defaults_setup(self):
    self :: A
    r0 :: int
    r1, r2 :: bool
L0:
    r0 = 10
    self.x = r0; r1 = is_error
    r2 = True
    return r2
def B.__mypyc_defaults_setup(self):
    self :: B
    r0 :: object
    r1 :: str
    r2 :: object
    r3 :: str
    r4 :: bool
    r5 :: None
    r6, r7, r8 :: bool
    r9 :: int
    r10, r11 :: bool
L0:
    r0 = __main__.globals :: static
    r1 = unicode_3 :: static  ('LOL')
    r2 = r0[r1] :: dict
    r3 = cast(str, r2)
    self.y = r3; r4 = is_error
    r5 = None
    self.z = r5; r6 = is_error
    r7 = True
    self.b = r7; r8 = is_error
    r9 = 10
    self.x = r9; r10 = is_error
    r11 = True
    return r11
