[case testTrivialFunction]
def f() -> int:
    return 1
[out]
def f():
    r0 :: int
L0:
    r0 = 1
    return r0

[case testFunctionArgument]
def f(x: int) -> int:
    return x
[out]
def f(x):
    x :: int
L0:
    return x

[case testExplicitNoneReturn]
def f() -> None:
    return
[out]
def f():
    r0 :: None
L0:
    r0 = None
    return r0

[case testExplicitNoneReturn2]
def f() -> None:
    return None
[out]
def f():
    r0 :: None
L0:
    r0 = None
    return r0

[case testAssignment]
def f() -> int:
    x = 1
    y = x
    return y
[out]
def f():
    x, y :: int
L0:
    x = 1
    y = x
    return y

[case testAssignmentTwice]
def f(x: int) -> None:
    y = 1
    y = x
    return
[out]
def f(x):
    x, y :: int
    r0 :: None
L0:
    y = 1
    y = x
    r0 = None
    return r0

[case testIntArithmetic]
def f(x: int, y: int) -> int:
    return x * (y + 1)
[out]
def f(x, y):
    x, y, r0, r1, r2 :: int
L0:
    r0 = 1
    r1 = y + r0 :: int
    r2 = x * r1 :: int
    return r2

[case testIf]
def f(x: int, y: int) -> int:
    if x < y:
        x = 1
    return x
[out]
def f(x, y):
    x, y :: int
L0:
    if x < y goto L1 else goto L2 :: int
L1:
    x = 1
L2:
    return x

[case testIfElse]
def f(x: int, y: int) -> int:
    if x < y:
        x = 1
    else:
        x = 2
    return x
[out]
def f(x, y):
    x, y :: int
L0:
    if x < y goto L1 else goto L2 :: int
L1:
    x = 1
    goto L3
L2:
    x = 2
L3:
    return x

[case testAnd]
def f(x: int, y: int) -> int:
    if x < y and x > y:
        x = 1
    else:
        x = 2
    return x
[out]
def f(x, y):
    x, y :: int
L0:
    if x < y goto L1 else goto L3 :: int
L1:
    if x > y goto L2 else goto L3 :: int
L2:
    x = 1
    goto L4
L3:
    x = 2
L4:
    return x

[case testOr]
def f(x: int, y: int) -> int:
    if x < y or x > y:
        x = 1
    else:
        x = 2
    return x
[out]
def f(x, y):
    x, y :: int
L0:
    if x < y goto L2 else goto L1 :: int
L1:
    if x > y goto L2 else goto L3 :: int
L2:
    x = 1
    goto L4
L3:
    x = 2
L4:
    return x

[case testSimpleNot]
def f(x: int, y: int) -> int:
    if not (x < y):
        x = 1
    return x
[out]
def f(x, y):
    x, y :: int
L0:
    if not x < y goto L1 else goto L2 :: int
L1:
    x = 1
L2:
    return x

[case testNotAnd]
def f(x: int, y: int) -> int:
    if not (x < y and x > y):
        x = 1
    return x
[out]
def f(x, y):
    x, y :: int
L0:
    if not x < y goto L2 else goto L1 :: int
L1:
    if not x > y goto L2 else goto L3 :: int
L2:
    x = 1
L3:
    return x

[case testWhile]
def f(x: int, y: int) -> int:
    while x > y:
        x = x - y
    return x
[out]
def f(x, y):
    x, y :: int
L0:
L1:
    if x > y goto L2 else goto L3 :: int
L2:
    x = x - y :: int
    goto L1
L3:
    return x

[case testWhile2]
def f(x: int, y: int) -> int:
    x = 1
    while x > y:
        x = x - y
    return x
[out]
def f(x, y):
    x, y :: int
L0:
    x = 1
L1:
    if x > y goto L2 else goto L3 :: int
L2:
    x = x - y :: int
    goto L1
L3:
    return x

[case testImplicitNoneReturn]
def f() -> None:
    pass
[out]
def f():
    r0 :: None
L0:
    r0 = None
    return r0

[case testImplicitNoneReturn2]
def f() -> None:
    x = 1
[out]
def f():
    x :: int
    r0 :: None
L0:
    x = 1
    r0 = None
    return r0

[case testImplicitNoneReturnAndIf]
def f(x: int, y: int) -> None:
    if x < y:
        x = 1
    else:
        y = 2
[out]
def f(x, y):
    x, y :: int
    r0 :: None
L0:
    if x < y goto L1 else goto L2 :: int
L1:
    x = 1
    goto L3
L2:
    y = 2
L3:
    r0 = None
    return r0

[case testRecursion]
def f(n: int) -> int:
    if n <= 1:
        return 1
    else:
        return f(n - 1) + f(n - 2)
[out]
def f(n):
    n, r0, r1, r2, r3, r4, r5, r6, r7, r8 :: int
L0:
    r0 = 1
    if n <= r0 goto L1 else goto L2 :: int
L1:
    r1 = 1
    return r1
L2:
    r2 = 1
    r3 = n - r2 :: int
    r4 = f(r3)
    r5 = 2
    r6 = n - r5 :: int
    r7 = f(r6)
    r8 = r4 + r7 :: int
    return r8
L3:
    unreachable

[case testReportTypeCheckError]
def f() -> None:
    return 1 # E: No return value expected

[case testReportSemanticaAnalysisError1]
def f(x: List[int]) -> None: pass # E: Name 'List' is not defined

[case testReportSemanticaAnalysisError2]
def f() -> None:
    x # E: Name 'x' is not defined

[case testElif]
def f(n: int) -> int:
    if n < 0:
        x = 1
    elif n == 0:
        x = 1
    else:
        x = 2
    return x
[out]
def f(n):
    n, r0, x, r1 :: int
L0:
    r0 = 0
    if n < r0 goto L1 else goto L2 :: int
L1:
    x = 1
    goto L6
L2:
    r1 = 0
    if n == r1 goto L3 else goto L4 :: int
L3:
    x = 1
    goto L5
L4:
    x = 2
L5:
L6:
    return x

[case testUnaryMinus]
def f(n: int) -> int:
    return -1
[out]
def f(n):
    n, r0, r1 :: int
L0:
    r0 = 1
    r1 = -r0 :: int
    return r1

[case testConditionalExpr]
def f(n: int) -> int:
    return 0 if n == 0 else 1
[out]
def f(n):
    n, r0, r1 :: int
L0:
    r0 = 0
    if n == r0 goto L1 else goto L2 :: int
L1:
    r1 = 0
    goto L3
L2:
    r1 = 1
L3:
    return r1

[case testOperatorAssignment]
def f() -> int:
    x = 0
    x += 1
    return x
[out]
def f():
    x, r0 :: int
L0:
    x = 0
    r0 = 1
    x = x + r0 :: int
    return x

[case testTrue]
def f() -> bool:
    return True
[out]
def f():
    r0 :: bool
L0:
    r0 = True
    return r0

[case testFalse]
def f() -> bool:
    return False
[out]
def f():
    r0 :: bool
L0:
    r0 = False
    return r0

[case testBoolCond]
def f(x: bool) -> bool:
    if x:
        return False
    else:
        return True
[out]
def f(x):
    x, r0, r1 :: bool
L0:
    if x goto L1 else goto L2 :: bool
L1:
    r0 = False
    return r0
L2:
    r1 = True
    return r1
L3:
    unreachable

[case testPycall]
import testmodule

def f(x: int) -> int:
    return testmodule.factorial(x)
[file testmodule.py]
def factorial(x: int) -> int:
    if x == 0:
        return 1
    else:
        return x * factorial(x-1)
[out]
def f(x):
    x :: int
    r0, r1, r2, r3 :: object
    r4 :: int
L0:
    r1 = module_testmodule :: static
    r0 = r1.factorial
    r3 = box(int, x)
    r2 = r0(r3) :: py
    r4 = unbox(int, r2)
    return r4

[case testPrintFullname]
import builtins
def f(x: int) -> None:
    builtins.print(5)
[out]
def f(x):
    x :: int
    r0, r1 :: object
    r2 :: int
    r3, r4 :: object
    r5, r6 :: None
L0:
    r1 = module_builtins :: static
    r0 = r1.print
    r2 = 5
    r4 = box(int, r2)
    r3 = r0(r4) :: py
    r5 = cast(None, r3)
    r6 = None
    return r6

[case testPrint]
import builtins
def f(x: int) -> None:
    print(5)
[out]
def f(x):
    x, r0 :: int
    r1, r2, r3, r4 :: object
    r5, r6 :: None
L0:
    r0 = 5
    r2 = module_builtins :: static
    r1 = r2.print
    r4 = box(int, r0)
    r3 = r1(r4) :: py
    r5 = cast(None, r3)
    r6 = None
    return r6

[case testUnicodeLiteral]
def f() -> str:
    x = "some string"
    return "some other string"
[out]
def f():
    x, r0 :: str
L0:
    x = __unicode_0 :: static
    r0 = __unicode_1 :: static
    return r0

[case testPyMethodCall1]
from typing import List
def f(x: List[int]) -> int:
    y = x.pop()
    return x.pop()
[out]
def f(x):
    x :: list
    y :: int
    r0 :: str
    r1 :: object
    r2 :: int
    r3 :: str
    r4 :: object
    r5 :: int
L0:
    r0 = __unicode_0 :: static
    r1 = x.r0() :: py
    r2 = unbox(int, r1)
    y = r2
    r3 = __unicode_0 :: static
    r4 = x.r3() :: py
    r5 = unbox(int, r4)
    return r5

[case testObjectType]
def g(y: object) -> None:
    g(y)
    g([1])
    g(None)
[out]
def g(y):
    y :: object
    r0 :: None
    r1 :: int
    r2 :: list
    r3 :: object
    r4, r5, r6, r7 :: None
L0:
    r0 = g(y)
    r1 = 1
    r3 = box(int, r1)
    r2 = [r3]
    r4 = g(r2)
    r5 = None
    r6 = g(r5)
    r7 = None
    return r7

[case testCoerceToObject1]
def g(y: object) -> object:
    g(1)
    a = [y]
    a[0] = (1, 2)
    y = True
    return 3
[out]
def g(y):
    y :: object
    r0 :: int
    r1, r2 :: object
    a :: list
    r3 :: int
    r4 :: tuple[int, int]
    r5, r6 :: int
    r7 :: object
    r8, r9 :: bool
    r10 :: int
    r11 :: object
L0:
    r0 = 1
    r2 = box(int, r0)
    r1 = g(r2)
    a = [y]
    r3 = 0
    r5 = 1
    r6 = 2
    r4 = (r5, r6)
    r7 = box(tuple[int, int], r4)
    r8 = a.__setitem__(r3, r7)
    r9 = True
    y = box(bool, r9)
    r10 = 3
    r11 = box(int, r10)
    return r11

[case testCoerceToObject2]
class A:
    x: object
    n: int
def f(a: A, o: object) -> None:
    a.x = 1
    o = a.n
[out]
def f(a, o):
    a :: A
    o :: object
    r0 :: int
    r1 :: object
    r2 :: bool
    r3 :: int
    r4 :: None
L0:
    r0 = 1
    r1 = box(int, r0)
    a.x = r1; r2 = is_error
    r3 = a.n
    o = box(int, r3)
    r4 = None
    return r4

[case testDownCast]
from typing import cast, List, Tuple
class A: pass
def f(x: object) -> None:
    n = cast(int, x)
    b = cast(bool, x)
    a = cast(A, x)
    l = cast(List[int], x)
    t = cast(Tuple[int, A], x)
[out]
def f(x):
    x :: object
    n :: int
    b :: bool
    a :: A
    l :: list
    t :: tuple[int, A]
    r0 :: None
L0:
    n = unbox(int, x)
    b = unbox(bool, x)
    a = cast(A, x)
    l = cast(list, x)
    t = unbox(tuple[int, A], x)
    r0 = None
    return r0

[case testDownCastSpecialCases]
from typing import cast, Optional, Tuple
class A: pass
def f(o: Optional[A], n: int, t: Tuple[int, ...]) -> None:
    a = cast(A, o)
    m = cast(bool, n)
    tt: Tuple[int, int]
    t = tt
[out]
def f(o, n, t):
    o :: optional[A]
    n :: int
    t :: tuple
    a :: A
    m :: bool
    r0 :: object
    tt :: tuple[int, int]
    r1 :: None
L0:
    a = cast(A, o)
    r0 = box(int, n)
    m = unbox(bool, r0)
    t = box(tuple[int, int], tt)
    r1 = None
    return r1

[case testSuccessfulCast]
from typing import cast, Optional, Tuple, List, Dict
class A: pass
def f(o: object,
      p: Optional[A],
      n: int,
      b: bool,
      t: Tuple[int, ...],
      s: Tuple[int, int],
      a: A,
      l: List[A],
      d: Dict[int, str]) -> None:
    o = cast(object, o)
    p = cast(Optional[A], p)
    n = cast(int, n)
    b = cast(bool, b)
    t = cast(Tuple[int, ...], t)
    s = cast(Tuple[int, int], s)
    o = cast(object, n)
    a = cast(A, a)
    l2 = cast(List[object], l)
    d2 = cast(Dict[object, str], d)
[out]
def f(o, p, n, b, t, s, a, l, d):
    o :: object
    p :: optional[A]
    n :: int
    b :: bool
    t :: tuple
    s :: tuple[int, int]
    a :: A
    l :: list
    d :: dict
    l2 :: list
    d2 :: dict
    r0 :: None
L0:
    o = o
    p = p
    n = n
    b = b
    t = t
    s = s
    o = box(int, n)
    a = a
    l2 = l
    d2 = d
    r0 = None
    return r0
